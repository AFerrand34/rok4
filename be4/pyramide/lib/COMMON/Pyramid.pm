# Copyright © (2011) Institut national de l'information
#                    géographique et forestière 
# 
# Géoportail SAV <geop_services@geoportail.fr>
# 
# This software is a computer program whose purpose is to publish geographic
# data using OGC WMS and WMTS protocol.
# 
# This software is governed by the CeCILL-C license under French law and
# abiding by the rules of distribution of free software.  You can  use, 
# modify and/ or redistribute the software under the terms of the CeCILL-C
# license as circulated by CEA, CNRS and INRIA at the following URL
# "http://www.cecill.info". 
# 
# As a counterpart to the access to the source code and  rights to copy,
# modify and redistribute granted by the license, users are provided only
# with a limited warranty  and the software's author,  the holder of the
# economic rights,  and the successive licensors  have only  limited
# liability. 
# 
# In this respect, the user's attention is drawn to the risks associated
# with loading,  using,  modifying and/or developing or reproducing the
# software by the user in light of its specific status of free software,
# that may mean  that it is complicated to manipulate,  and  that  also
# therefore means  that it is reserved for developers  and  experienced
# professionals having in-depth computer knowledge. Users are therefore
# encouraged to load and test the software's suitability as regards their
# requirements in conditions enabling the security of their systems and/or 
# data to be ensured and,  more generally, to use and operate it in the 
# same conditions as regards security. 
# 
# The fact that you are presently reading this means that you have had
# 
# knowledge of the CeCILL-C license and that you accept its terms.

################################################################################

=begin nd
File: Pyramid.pm

Class: COMMON::Pyramid

Store all informations about a pyramid.

Using:
    (start code)
    use COMMON::Pyramid;

    (end code)

Attributes:
    name - string - Pyramid's name
    desc_path - string - Directory in which we write the pyramid's descriptor
    data_path - string - Directory in which we write the pyramid's data
    content_path - string - Path to the content's list

    own_masks - boolean - If TRUE, masks generated by tools will be written in the final pyramid. If we want to export them, we have to use them in tools.
    image_width - integer - Number of tile in an pyramid's image, widthwise.
    image_height - integer - Number of tile in an pyramid's image, heightwise.

    pyrImgSpec - <PyrImageSpec> - Pyramid's image's components
    tms - <TileMatrixSet> - Pyramid's images will be cutted according to this TMS grid.
    nodata - string - Nodata color
    levels - <Level> hash - Key is the level ID, the value is the <Level> object. Define levels present in the pyramid.

    dir_depth - integer - Number of subdirectories from the level root to the image : depth = 2 => /.../LevelID/SUB1/SUB2/IMG.tif
    dir_image - string - Name of images' directory
    dir_mask - string - Name of masks' directory

=cut

################################################################################

package COMMON::Pyramid;

use strict;
use warnings;

use Log::Log4perl qw(:easy);
use XML::LibXML;

use File::Spec::Link;
use File::Basename;
use File::Spec;
use File::Path;
use File::Copy;
use Tie::File;

use Data::Dumper;

use COMMON::Level;
use COMMON::NoData;
use COMMON::PyrImageSpec;

require Exporter;
use AutoLoader qw(AUTOLOAD);

our @ISA = qw(Exporter);

our %EXPORT_TAGS = ( 'all' => [ qw() ] );
our @EXPORT_OK   = ( @{$EXPORT_TAGS{'all'}} );
our @EXPORT      = qw();

################################################################################
# Constantes
use constant TRUE  => 1;
use constant FALSE => 0;

################################################################################

BEGIN {}

INIT {}

END {}

####################################################################################################
#                                        Group: Constructors                                       #
####################################################################################################

=begin nd
Constructor: new

Pyramid constructor. Bless an instance.

Parameters (list):
    type - string - DESCRIPTOR or VALUES
    params - hash - All parameters about the new pyramid, "pyramid" section of the be4 configuration file
See also:
    <_init>, <_load>
=cut
sub new {
    my $class = shift;
    my $type = shift;
    my $params = shift;

    $class = ref($class) || $class;

    # IMPORTANT : if modification, think to update natural documentation (just above)

    my $this = {
        type => undef,

        name => undef,
        desc_path => undef,
        content_path  => undef,

        # OUT
        image_width  => undef,
        image_height => undef,
        own_masks => FALSE,

        pyrImgSpec => undef,
        tms => undef,
        nodata => undef,
        levels => {},

        # Pyramide FICHIER
        data_path => undef,
        dir_depth => undef,

        # Pyramide S3
        data_bucket => undef,
        s3_api_url => undef,
        s3_key => undef,
        s3_secret_key => undef,

        # Pyramide CEPH
        data_pool => undef,
        cluster_name => undef,
        user_name => undef,
        conf_file => undef
    };

    bless($this, $class);

    if ($type eq "DESCRIPTOR") {
        # Le paramètre est le chemin du descripteur de pyramide, on en tire 'name' et 'desc_path'
        if (! -f $params) {
            ERROR ("XML file does not exist: $params !");
            return FALSE;
        }

        # Cette pyramide est donc en lecture
        $this->{type} = "READ";

        $this->{name} = File::Basename::basename($params);
        $this->{name} =~ s/\.(pyr|PYR)$//;
        $this->{desc_path} = File::Basename::dirname($params);

        # On remplit params avec les paramètres issus du parsage du XML
        $params = {};
        $params->{desc_path} = $this->{desc_path};
        $params->{name} = $this->{name};
        if (! $this->_readDescriptor($params)) {
            ERROR ("The parameter 'name' is required !");
            return undef;
        }

    } else {
        # On crée une pyramide à partir de ses caractéristiques
        # Cette pyramide est donc une nouvelle pyramide, à écrire
        $this->{type} = "WRITE";

        # Pyramid pyr_name_new, desc path
        if (! exists $params->{pyr_name_new} || ! defined $params->{pyr_name_new}) {
            ERROR ("The parameter 'pyr_name_new' is required!");
            return undef;
        }
        $this->{name} = $params->{pyr_name_new};
        $this->{name} =~ s/\.(pyr|PYR)$//;

        if (! exists $params->{pyr_desc_path} || ! defined $params->{pyr_desc_path}) {
            ERROR ("The parameter 'pyr_desc_path' is required!");
            return undef;
        }
        $this->{desc_path} = $params->{pyr_desc_path};

        if (exists $params->{data_path} && defined $params->{pyr_data_path}) {

            #### CAS D'UNE PYRAMIDE FICHIER
            $this->{data_path} = $params->{pyr_data_path};

            # dir_depth
            if (! exists $params->{dir_depth} || ! defined $params->{dir_depth}) {
                ERROR ("The parameter 'dir_depth' is required!");
                return undef;
            }
            $this->{dir_depth} = $params->{dir_depth};
        }

        elsif (exists $params->{pyr_data_bucket_name} && defined $params->{pyr_data_bucket_name}) {

            #### CAS D'UNE PYRAMIDE S3
            $this->{data_bucket} = $params->{pyr_data_bucket_name};

            if (! exists $params->{pyr_data_api_url} || ! defined $params->{pyr_data_api_url}) {
                ERROR ("The parameter 'pyr_data_api_url' is required!");
                return undef;
            }
            $this->{s3_api_url} = $params->{pyr_data_api_url};
            
            if (! exists $params->{pyr_data_key} || ! defined $params->{pyr_data_key}) {
                ERROR ("The parameter 'pyr_data_key' is required!");
                return undef;
            }
            $this->{s3_key} = $params->{pyr_data_key};
            
            if (! exists $params->{pyr_data_secret_key} || ! defined $params->{pyr_data_secret_key}) {
                ERROR ("The parameter 'pyr_data_secret_key' is required!");
                return undef;
            }
            $this->{s3_secret_key} = $params->{pyr_data_secret_key};

        }

        elsif (exists $params->{pyr_data_pool_name} && defined $params->{pyr_data_pool_name}) {

            #### CAS D'UNE PYRAMIDE CEPH
            $this->{data_pool} = $params->{pyr_data_pool_name};

            if (! exists $params->{pyr_data_cluster_name} || ! defined $params->{pyr_data_cluster_name}) {
                ERROR ("The parameter 'pyr_data_cluster_name' is required!");
                return undef;
            }
            $this->{cluster_name} = $params->{pyr_data_cluster_name};
            
            if (! exists $params->{pyr_data_user_name} || ! defined $params->{pyr_data_user_name}) {
                ERROR ("The parameter 'pyr_data_user_name' is required!");
                return undef;
            }
            $this->{user_name} = $params->{pyr_data_user_name};
            
            if (! exists $params->{pyr_data_conf_file} || ! defined $params->{pyr_data_conf_file}) {
                ERROR ("The parameter 'pyr_data_conf_file' is required!");
                return undef;
            }
            $this->{conf_file} = $params->{pyr_data_conf_file};

        }

        else {
            ERROR("No storage provided for the new pyramid");
            return undef;
        }

    }

    if ( ! $this->_load($params) ) {return undef;}

    return $this;
}

=begin nd
Function: _load
=cut
sub _load {
    my $this   = shift;
    my $params = shift;

    if (! defined $params ) {
        ERROR ("Parameters argument required (null) !");
        return FALSE;
    }

    # TMS

    if (! exists $params->{tms_name} || ! defined $params->{tms_name}) {
        ERROR ("The parameter 'tms_name' is required!");
        return FALSE;
    }
    # On chargera l'objet TMS plus tard on ne mémorise pour le moment que son nom.
    $this->{tms} = $params->{tms_name};
    $this->{tms} =~ s/\.TMS$//i;
    
    # image_width
    if (! exists $params->{image_width} || ! defined $params->{image_width}) {
        ERROR ("The parameter 'image_width' is required!");
        return FALSE;
    }
    $this->{image_width} = $params->{image_width};

    # image_height
    if (! exists $params->{image_height} || ! defined $params->{image_height}) {
        ERROR ("The parameter 'image_height' is required!");
        return FALSE;
    }
    $this->{image_height} = $params->{image_height};

    # We want masks in the final pyramid ?
    if ( exists $params->{export_masks} && defined $params->{export_masks} && uc($params->{export_masks}) eq "TRUE" ) {
        $this->{own_masks} = TRUE;
    }

    # PyrImageSpec
    my $pyrImgSpec = COMMON::PyrImageSpec->new($params);

    if (! defined $pyrImgSpec) {
        ERROR ("Can not load specification of pyramid's images !");
        return FALSE;
    }

    $this->{pyrImgSpec} = $pyrImgSpec;

    # NoData
    if (! exists $params->{color} || ! defined $params->{color}) {
        ERROR ("The parameter 'color' is required!");
        return FALSE;
    }
    ##### create NoData !
    my $objNodata = COMMON::NoData->new({
        pixel   => $this->{pyrImgSpec}->getPixel(),
        value   => $params->{color},
    });

    if (! defined $objNodata) {
        ERROR ("Can not load NoData !");
        return FALSE;
    }
    $this->{nodata} = $objNodata;

    return TRUE;
}


=begin nd
Function: _readDescriptor
=cut
sub _readDescriptor {
    my $this   = shift;
    my $params = shift;

    my $pyrDescFile = File::Spec->catfile($this->{desc_path},$this->{name}.".pyr");

    # read xml pyramid
    my $parser  = XML::LibXML->new();
    my $xmltree =  eval { $parser->parse_file($pyrDescFile); };

    if (! defined ($xmltree) || $@) {
        ERROR (sprintf "Can not read the XML file $pyrDescFile : %s !", $@);
        return FALSE;
    }

    my $root = $xmltree->getDocumentElement;

    # read tag value of nodata value, photometric and interpolation (not obligatory)

    # NODATA
    my $tagnodata = $root->findnodes('nodataValue')->to_literal;
    if ($tagnodata eq '') {
        ERROR (sprintf "Can not extract 'nodata' from the XML file $pyrDescFile !");
        return FALSE;
    }
    $params->{color} = $tagnodata;
    
    # PHOTOMETRIC
    my $tagphotometric = $root->findnodes('photometric')->to_literal;
    if ($tagphotometric eq '') {
        ERROR (sprintf "Can not extract 'photometric' from the XML file $pyrDescFile !");
        return FALSE;
    }
    $params->{photometric} = $tagphotometric;

    # INTERPOLATION    
    my $taginterpolation = $root->findnodes('interpolation')->to_literal;
    if ($taginterpolation eq '') {
        ERROR (sprintf "Can not extract 'interpolation' from the XML file $pyrDescFile !");
        return FALSE;
    }
    $params->{interpolation} = $taginterpolation;

    # Read tag value of tileMatrixSet, format and channel, MANDATORY

    # TMS
    my $tagtmsname = $root->findnodes('tileMatrixSet')->to_literal;
    if ($tagtmsname eq '') {
        ERROR (sprintf "Can not extract 'tileMatrixSet' from the XML file $pyrDescFile !");
        return FALSE;
    }
    $params->{tms_name} = $tagtmsname;

    # FORMAT
    my $tagformat = $root->findnodes('format')->to_literal;
    if ($tagformat eq '') {
        ERROR (sprintf "Can not extract 'format' in the XML file $pyrDescFile !");
        return FALSE;
    }

    my ($comp, $sf, $bps) = COMMON::PyrImageSpec::decodeFormat($tagformat);
    if (! defined $comp) {
        ERROR (sprintf "Can not decode the pyramid format");
        return FALSE;
    }

    $params->{sampleformat} = $sf;
    $params->{compression} = $comp;
    $params->{bitspersample} = $bps;

    # SAMPLESPERPIXEL  
    my $tagsamplesperpixel = $root->findnodes('channels')->to_literal;
    if ($tagsamplesperpixel eq '') {
        ERROR (sprintf "Can not extract 'channels' in the XML file $pyrDescFile !");
        return FALSE;
    } 
    $params->{samplesperpixel} = $tagsamplesperpixel;

    # load pyramid level
    my @levels = $root->getElementsByTagName('level');

    my $oneLevelId;
    foreach my $v (@levels) {

        my $tagtm = $v->findvalue('tileMatrix');

        my $objLevel = COMMON::Level->new("XML", $v, $this->{desc_path});
        if (! defined $objLevel) {
            ERROR(sprintf "Can not load the pyramid level : '%s'", $tagtm);
            return FALSE;
        }

        $this->{levels}->{$tagtm} = $objLevel;

        $oneLevelId = $tagtm;

        # same for each level
    }

    if (defined $oneLevelId) {
        $params->{image_width}  = $this->{levels}->{$oneLevelId}->getImageWidth();
        $params->{image_height} = $this->{levels}->{$oneLevelId}->getImageHeight();

        if ($this->{levels}->{$oneLevelId}->ownMasks()) {
            $params->{export_masks} = "TRUE";
        }
    } else {
        # On a aucun niveau dans la pyramide à charger, il va donc nous manquer des informations : on sort en erreur
        ERROR("No level in the pyramid's descriptor $pyrDescFile");
        return FALSE;
    }

    return TRUE;
}

####################################################################################################
#                                        Group: Update pyramid                                     #
####################################################################################################

sub bindTileMatrixSet {
    my $this = shift;
    my $tmsPath = shift;

    # 1 : Créer l'objet TileMatrixSet
    my $tmsFile = File::Spec->catdir($tmsPath, $this->{tms}.".tms");
    $this->{tms} = COMMON::TileMatrixSet->new($tmsFile);
    if (! defined $this->{tms}) {
        ERROR("Cannot create a TileMatrixSet object from the file $tmsFile");
        return FALSE;
    }

    # 2 : Lier le TileMatrix à chaque niveau de la pyramide
    while (my ($id, $level) = each(%{$this->{levels}}) ) {
        if (! $level->bindTileMatrix($this->{tms})) {
            ERROR("Cannot bind a TileMatrix to pyramid's level $id");
            return FALSE;
        }
    }

    return TRUE;
}

sub addLevel {
    my $this = shift;
    my $level = shift;

    if ($this->{type} eq "READ") {
        ERROR("Cannot add level to 'read' pyramid");
        return FALSE;        
    }

    if (exists $this->{levels}->{$level}) {
        ERROR("Cannot add level $level in pyramid : already exists");
        return FALSE;
    }

    my $levelParams = {};
    if (defined $this->{data_path}) {
        # On doit ajouter un niveau stockage fichier
        $levelParams = {
            id => $level,
            tm => $this->{tms}->getTileMatrix($level),
            size => [$this->{image_width}, $this->{image_height}],

            dir_image => File::Spec->catdir($this->{desc_path}, $this->{name}, $this->{dir_image}, $level),
            dir_depth => $this->{dir_depth}
        };

        if ($this->{own_masks}) {
            $levelParams->{dir_mask} = File::Spec->catdir($this->{desc_path}, $this->{name}, $this->{dir_mask}, $level);
        }
    }
    elsif (defined $this->{data_pool}) {
        # On doit ajouter un niveau stockage ceph
        $levelParams = {
            id => $level,
            tm => $this->{tms}->getTileMatrix($level),
            size => [$this->{image_width}, $this->{image_height}],

            prefix => $this->{name},
            pool_name => $this->{data_pool}
        };

        if ($this->{own_masks}) {
            $levelParams->{hasMask} = TRUE;
        }
    }
    elsif (defined $this->{data_bucket}) {
        # On doit ajouter un niveau stockage s3
        $levelParams = {
            id => $level,
            tm => $this->{tms}->getTileMatrix($level),
            size => [$this->{image_width}, $this->{image_height}],

            prefix => $this->{name},
            pool_name => $this->{data_bucket}
        };

        if ($this->{own_masks}) {
            $levelParams->{hasMask} = TRUE;
        }
    }


    $this->{levels}->{$level} = COMMON::Level->new("VALUES", $levelParams, $this->{desc_path});

    if (! defined $this->{levels}->{$level}) {
        ERROR("Cannot create a Level object for level $level");
        return FALSE;
    }

    return TRUE;
}

####################################################################################################
#                                      Group: Pyramids comparison                                  #
####################################################################################################

=begin nd
Function: checkConsistency

We control values, in order to have the same as the final pyramid.

Compatibility = it's possible to convert (different compression or samples per pixel).

Equals = all format's parameters are the same (not the content).

Return 0 if pyramids is not consistent, 1 if compatibility but not equals, 2 if equals

Parameters (list):
    other - <COMMON::Pyramid> - Pyramid to compare
=cut
sub checkCompatibility {
    my $this = shift;
    my $other = shift;
    my $strict = shift;

    if ($this->getTilesPerWidth() != $other->getTilesPerWidth()) {
        return 0;
    }
    if ($this->getTilesPerHeight() != $other->getTilesPerHeight()) {
        return 0;
    }
    if ($this->getDirDepth() != $other->getDirDepth()) {
        return 0;
    }

    if ($this->getTileMatrixSet()->getName() ne $other->getTileMatrixSet()->getName()) {
        return 0;
    }

    if ($this->getImageSpec()->getPixel()->getSampleFormat() ne $other->getImageSpec()->getPixel()->getSampleFormat()) {
        return 0;
    }
    if ($this->getImageSpec()->getPixel()->getBitsPerSample() ne $other->getImageSpec()->getPixel()->getBitsPerSample()) {
        return 0;
    }

    # Photometric; samplesperpixel et compression peuvent être différent, on garde la compatibilité
    if ($this->getImageSpec()->getPixel()->getPhotometric() ne $other->getImageSpec()->getPixel()->getPhotometric()) {
        return 1;
    }
    if ($this->getImageSpec()->getPixel()->getSamplesPerPixel() ne $other->getImageSpec()->getPixel()->getSamplesPerPixel()) {
        return 1;
    }
    if ($this->getImageSpec()->getCompression() ne $other->getImageSpec()->getCompression()) {
        return 1;
    }

    return 2;
}

####################################################################################################
#                                Group: Common getters                                             #
####################################################################################################

# Function: ownMasks
sub ownMasks {
    my $this = shift;
    return $this->{own_masks};
}

# Function: getName
sub getName {
    my $this = shift;    
    return $this->{name};
}

# Function: getDescriptorFile
sub getDescriptorFile {
    my $this = shift;    
    return File::Spec->catfile($this->{desc_path}, $this->{name}.".pyr");
}

# Function: getDescriptorDir
sub getDescriptorDir {
    my $this = shift;    
    return $this->{desc_path};
}

# Function: getListFile
sub getListFile {
    my $this = shift;
    
    if (! defined $this->{content_path}) {
        $this->{content_path} = File::Spec->catfile($this->{desc_path}, $this->{name}.".list");
    }
    
    return $this->{content_path};
}


# Function: getTileMatrixSet
sub getTileMatrixSet {
    my $this = shift;
    return $this->{tms};
}

# Function: getImageSpec
sub getImageSpec {
    my $this = shift;
    return $this->{pyrImgSpec};
}

# Function: getNodata
sub getNodata {
    my $this = shift;
    return $this->{nodata};
}

=begin nd
Function: getSlabPath

Returns the theoric slab path, undef if the level is not present in the pyramid

Parameters (list):
    type - string - IMGAGE, MASK
    level - string - Level ID
    col - integer - Slab column
    row - integer - Slab row
=cut
sub getSlabPath {
    my $this = shift;
    my $type = shift;
    my $level = shift;
    my $col = shift;
    my $row = shift;

    if (! exists $this->{levels}->{$level}) {
        return undef;
    }

    return $this->{levels}->{$level}->getSlabPath($type, $col, $row);
}

# Function: getTilesPerWidth
sub getTilesPerWidth {
    my $this = shift;
    return $this->{image_width};
}

# Function: getTilesPerHeight
sub getTilesPerHeight {
    my $this = shift;
    return $this->{image_height};
}

# Function: getLevel
sub getLevel {
    my $this = shift;
    my $level = shift;
    return $this->{levels}->{$level};
}


####################################################################################################
#                                Group: Storage getters                                            #
####################################################################################################

# Function: getStorageType
sub getStorageType {
    my $this = shift;

    if ($this->{type} eq "READ") {
        # Potentiellement, les niveaux n'ont pas tous le même type de stockage, on ne répond pas
        return undef;
    }

    if (defined $this->{data_path}) {
        return "FILE";
    }
    elsif (defined $this->{data_pool}) {
        return "CEPH";
    }
    elsif (defined $this->{data_bucket}) {
        return "S3";
    }
}

### FILE

# Function: getDataDir
sub getDataDir {
    my $this = shift;    
    return File::Spec->catfile($this->{data_path}, $this->{name});
}

# Function: getDirDepth
sub getDirDepth {
    my $this = shift;
    return $this->{dir_depth};
}

### S3

# Function: getDataBucket
sub getDataBucket {
    my $this = shift;    
    return $this->{data_bucket};
}

# Function: getApiUrl
sub getApiUrl {
    my $this = shift;    
    return $this->{s3_api_url};
}

# Function: getKey
sub getKey {
    my $this = shift;    
    return $this->{s3_key};
}

# Function: getSecretKey
sub getSecretKey {
    my $this = shift;    
    return $this->{s3_secret_key};
}

### CEPH

# Function: getDataPool
sub getDataPool {
    my $this = shift;    
    return $this->{data_pool};
}

# Function: getClusterName
sub getClusterName {
    my $this = shift;    
    return $this->{cluster_name};
}

# Function: getUserName
sub getUserName {
    my $this = shift;    
    return $this->{user_name};
}

# Function: getConfFile
sub getConfFile {
    my $this = shift;    
    return $this->{conf_file};
}

####################################################################################################
#                                      Group: Write  functions                                     #
####################################################################################################

sub writeDescriptor {
    my $this = shift;

    if ($this->{type} eq "READ") {
        ERROR("Cannot write descriptor of 'read' pyramid");
        return FALSE;        
    }

    my $descPath = File::Spec->catdir($this->{desc_path}, $this->{name}.".pyr");

    if (-f $descPath) {
        ERROR("Cannot owerwrite pyramid descriptor $descPath");
        return FALSE;
    }

    if (! open FILE, ">", $descPath ){
        ERROR(sprintf "Cannot open the pyramid descriptor %s to write",$descPath);
        return FALSE;
    }

    my $string = "<?xml version='1.0' encoding='UTF-8'?>\n";
    $string .= "<Pyramid>\n";
    $string .= sprintf "    <tileMatrixSet>%s</tileMatrixSet>\n", $this->{tms}->getName();
    $string .= sprintf "    <format>%s</format>\n", $this->{pyrImgSpec}->getFormatCode();
    $string .= sprintf "    <channels>%s</channels>\n", $this->{pyrImgSpec}->getPixel()->getSamplesPerPixel();
    $string .= sprintf "    <nodataValue>%s</nodataValue>\n", $this->{nodata}->getValue();
    $string .= sprintf "    <interpolation>%s</interpolation>\n", $this->{pyrImgSpec}->getInterpolation();
    $string .= sprintf "    <photometric>%s</photometric>\n", $this->{pyrImgSpec}->getPixel()->getPhotometric();


    my @orderedLevels = sort {$a->getOrder <=> $b->getOrder} ( values %{$this->{levels}});

    for (my $i = scalar @orderedLevels - 1; $i >= 0; $i--) {
        # we write levels in pyramid's descriptor from the top to the bottom
        $string .= $orderedLevels[$i]->exportToXML();
    }

    $string .= "</Pyramid>";

    print FILE $string;

    close(FILE);

    return TRUE
}

sub writeList {
    my $this = shift;
    my $forest = shift;
    my $ancestor = shift;

    if ($this->{type} eq "READ") {
        ERROR("Cannot write list of 'read' pyramid");
        return FALSE;        
    }

    if (! defined $forest || ref ($ancestor) ne "COMMON::Forest" ) {
        ERROR(sprintf "We need a COMMON::Forest to write pyramid list ! ");
        return FALSE;
    }

    if (defined $ancestor && ref ($ancestor) ne "COMMON::Pyramid" ) {
        ERROR(sprintf "Ancestor, if provided, have to be a COMMON::Pyramid ! ");
        return FALSE;
    }

    my $filePath = $this->getListFile();

    if (-f $filePath ) {
        ERROR(sprintf "New pyramid list ('%s') exist, can not overwrite it ! ", $filePath);
        return FALSE;
    }
    
    if (! defined $ancestor) {
        # Pas d'ancêtre, on doit juste écrire l'en tête : le dossier propre à cette pyramide ou le nom du conteneur objet
        my $dir = File::Basename::dirname($filePath);
        if (! -d $dir) {
            eval { mkpath([$dir]); };
            if ($@) {
                ERROR(sprintf "Can not create the pyramid list directory '%s' : %s !", $dir , $@);
                return FALSE;
            }
        }
        
        my $LIST;

        if (! open $LIST, ">", $filePath) {
            ERROR(sprintf "Cannot open new pyramid list file : %s",$filePath);
            return FALSE;
        }
        
        if (defined $this->{data_path}) {
            printf $LIST "0=%s\n", $this->getDataDir();
        }
        elsif (defined $this->{data_pool}) {
            printf $LIST "%s\n", $this->{data_pool};
        }
        elsif (defined $this->{data_bucket}) {
            printf $LIST "%s\n", $this->{data_bucket};
        }

        printf $LIST "#\n";
        close $LIST;

        return TRUE
    }

    ERROR("Pour l'instant on ne gère pas les mises à jour avec cette classe");

    return FALSE;
}


1;
__END__
